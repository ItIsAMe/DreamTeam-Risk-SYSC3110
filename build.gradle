plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

mainClassName = "com.dreamteam.view.HomeScreenFrame"

group 'com.dreamteam'
version '3.0-SNAPSHOT'

sourceCompatibility = 15

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    jaxDoclet
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'com.github.cliftonlabs:json-simple:3.1.0'
}

test {
    useJUnitPlatform()

    maxHeapSize = '1G'
}

shadowJar {
    from sourceSets.main.java //add java files
    from './worlds' //./extra_files/worlds -> ./worlds
}

task javadocs(type: Javadoc) {
    source = sourceSets.main.java.srcDirs
    classpath = sourceSets.main.runtimeClasspath
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task deployDocs(type: Copy) {
    dependsOn javadocs
    from "build/docs/javadoc"
    into "build/deployFull/documentation"
}

task deployFull(type: Copy) {
    dependsOn deployDocs
    dependsOn shadowJar
    from "build/libs"
    into "build/deployFull"
}

task zipDeployment(type: Zip) {
    dependsOn deployFull
    from 'build/deployFull'
    include '*'
    include '*/*'
    archiveName 'Deployment.zip'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}